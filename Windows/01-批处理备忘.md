## 文件路径相关处理
下面的批处理脚本可以获取文件所在路径、文件名、文件后缀名等信息。
>注意要用 GBK 或者 GB18030 编码。
```batch
@echo off

set /p filepath="Please input file path: "
echo %filepath%
for %%f in (%filepath%) do (
    set filename=%%~nf
    set extname=%%~xf
    set dirname=%%~dpf
    set fullname=%%~nxf
    set noextpathname=%%~dpnf
)

echo 文件名:%filename%
echo 后缀名:%extname%
echo 所在目录:%dirname%
echo 完整文件名:%fullname%
echo 无后缀名路径:%noextpathname%
pause
```

- 输出示例1：
```
Please input file path: D:\Download\Country.mmdb
D:\Download\Country.mmdb
文件名:Country
后缀名:.mmdb
所在目录:D:\Download\
完整文件名:Country.mmdb
无后缀名路径:D:\Download\Country
请按任意键继续. . .
```

- 输出示例2(**路径中有空格**)：
```
Please input file path: "D:\Tencent Files\WXWork\Global\Config.cfg"
"D:\Tencent Files\WXWork\Global\Config.cfg"
文件名:Config
后缀名:.cfg
所在目录:D:\Tencent Files\WXWork\Global\
完整文件名:Config.cfg
无后缀名路径:D:\Tencent Files\WXWork\Global\Config
请按任意键继续. . .
```

## 变量的判断
变量的判断一般有：变量是否定义、变量是否为空。需要注意的是，判断变量是否定义**不等于**判断变量是否为空值。

它们的判断方式如下：
- 变量是否定义
    ```
    @echo off
    if not defined var (
        echo 变量未定义
    ) else (
        echo 变量已定义
    )
    pause
    ```

- 变量是否为空
    ```
    @echo off

    set var=
    if "%var%" == "" (
        echo 变量已定义但为空值
    )
    pause
    ```

## 递归遍历文件夹
若有文件夹目录结构如下：
```
.
├── all.pb
├── common.proto
├── protoc.bat
├── protoc.exe
└── type
    └── player.proto
```

现在要遍历该文件夹下的所有 `.proto` 文件，批处理如下：
```batch
@echo off

setlocal enabledelayedexpansion
set all_proto_filenames=common.proto
for /r "%~p0" %%i in ("*.proto") do (
    if  not %%~nxi==common.proto (
        set "pth=%%~pnxi"
        set all_proto_filenames=!all_proto_filenames! !pth:%~p0=!
    )
)
echo %all_proto_filenames%
pause
```

其中 `!pth:%~p0=!` 表示将绝对路径转换成相对路径。另外，for 循环中若要 echo 变量(for中set的变量)，则需要开启延迟环境变量。
关于**延迟环境变量**，可参考这篇文章[迟环境变量的理解](https://www.cnblogs.com/baiqiantao/p/12099516.html#%E8%BF%9F%E7%8E%AF%E5%A2%83%E5%8F%98%E9%87%8F%E7%9A%84%E7%90%86%E8%A7%A3)。

## 循环输入检查
```batch
set isPush=""
:LOOP
set /p push="是否预热CDN[Y/N]:"
if "%push%"=="Y" (
    set isPush="--push"
    goto UPLOAD
)
if "%push%"=="N" goto UPLOAD
echo 输入错误,请选择Y或N
goto LOOP

:UPLOAD
echo 开始预热CDN
```

## 设置全局环境变量并生效
运行以下脚本后，要重启新的终端，如果依然使用旧的 CMD 终端，是不会生效的。
```batch
@echo off

:INPUT
set /p env_name="环境变量名："
set /p env_value="环境变量值："

if "%env_name%" == "" (
    echo 请输入环境变量名称
    goto INPUT
)

setx %env_name% %env_value%

echo 重启资源管理...
taskkill /f /im explorer.exe
start explorer.exe
pause
```

## 将命令结果赋值给变量
下面分别描述通过批处理和命令提示符设置 `git merge ff` 的方式：
- 在 bat 脚本中
    ```batch
    for /F %%i in ('git branch --show-current') do (set curBranch=%%i)
    if "%curBranch%" == "master" (
        git config merge.ff true
    ) else (
        git config merge.ff false
    )
    ```

- 在 cmd 中
    ```batch
    for /F %i in ('git branch --show-current') do (set curBranch=%i)
    if "%curBranch%" == "master" (
        git config merge.ff true
    ) else (
        git config merge.ff false
    )
    ```